apply plugin: 'android-sdk-manager'
apply plugin: 'com.android.library'
apply plugin: 'org.robolectric'
apply plugin: 'maven'

group = 'com.stanfy.enroscar'
version = '2.0.1-SNAPSHOT'
archivesBaseName = 'enroscar-goro'

android {
  compileSdkVersion project.androidCompileSdkVersion
  buildToolsVersion project.androidBuildToolsVersion

  defaultConfig {
    minSdkVersion 8
    consumerProguardFiles file("config/consumer.pro")
    versionName project.version
  }

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_7
    targetCompatibility JavaVersion.VERSION_1_7
  }

  packagingOptions {
    exclude 'LICENSE'
    exclude 'NOTICE'
    exclude 'asm-license.txt'
  }
}

repositories {
  maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
}

dependencies {
  provided 'com.stanfy.enroscar:enroscar-async-interface:2.0-RC2-SNAPSHOT'
  provided 'io.reactivex:rxjava:1.0.13'

  def testDeps = [
    'org.mockito:mockito-all:1.9.5',
    'org.assertj:assertj-core:1.7.1',
    'com.squareup.assertj:assertj-android:1.0.1'
  ]

  testCompile 'junit:junit:4.12'
  testCompile testDeps
  testCompile 'org.robolectric:robolectric:3.0'

  androidTestCompile testDeps
  androidTestCompile 'io.reactivex:rxjava:1.0.13'
  androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2', {
    exclude group: 'org.hamcrest'
  }
}

// POM file modifications
uploadArchives {
  repositories {
    mavenDeployer {
      pom.project {
        name 'Goro'
        description 'Android laibrary for handling tasks in multiple queues'
        url 'https://github.com/stanfy/goro'
        inceptionYear '2013'

        scm {
          url 'https://github.com/stanfy/goro'
          connection 'scm:git:git@github.com:stanfy/goro.git'
          developerConnection 'scm:git:git@github.com:stanfy/goro.git'
        }

        licenses {
          license {
            name 'The Apache Software License, Version 2.0'
            url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
            distribution 'repo'
          }
        }

        developers {
          developer {
            id 'rmazur'
            name 'Roman Mazur'
          }
        }
      }
    }
  }
}
